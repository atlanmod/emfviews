grammar monoge.dsl.Extension with org.eclipse.xtext.common.Terminals
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate extension "http://www.dsl.monoge/Extension"

Model:'define' extensionName=ID 'extending' metamodel+=Metamodel ':' 
	prefix+=Prefix ("," metamodel+=Metamodel ':'  prefix+=Prefix)* 
	'{' extensions += Extension* '}';	
Extension: Create | Refine | Generalize | ModifyClass | FilterClass;
Metamodel:	name=ID;
Prefix:	name=ID;
Create:	'add class' class=ID;
Refine:	'add class' classNew=ID 'specializing' prefix=[Prefix] '.'
	classOriginal=ID;
Generalize:	'add class' classNew=ID 'supertyping' prefix+=[Prefix] 
	'.' class+=ID ("," prefix+=[Prefix] '.' class+=ID)*;
ModifyClass:
	'modify class' prefix=[Prefix] '.' class=ID '{'
		modifyOperators += ModifyOperator*
	'}';
ModifyOperator:	AddProperty | ModifyProperty | FilterProperty |
	AddConstraint | FilterConstraint;
AddProperty:	'add property' property=ID 'type' type=ID;
ModifyProperty:	'modify property' property=ID value+=ValueAssignment
	("," value+=ValueAssignment)*;
ValueAssignment:	attribute=ID '=' value=EString;
FilterProperty:	'filter property' property=ID;
FilterClass:	'filter class' prefix=[Prefix] '.' class=ID;
AddConstraint:	'add constraint' constraint=ID value=EString;
FilterConstraint:	'filter constraint' constraint=EString;
EString returns ecore::EString:	STRING | ID;

