-- @atlcompiler emftvm
-- @nsURI VPDL=http://www.inria.fr/atlanmod/emfviews/vpdl
-- @nsURI VL=http://www.inria.fr/virtualLinks

module VPDL2VirtualLinks;
create OUT : VirtualLinks from IN : VPDL;

rule Model2VirtualLinks {
  from
  	s : VPDL!View
  to
  	t : VirtualLinks!WeavingModel (
		name <- s.name,
		whitelist <- true,
		contributingModels <- s."from".metamodels,
		virtualLinks <- VPDL!Feature.allInstances()
    )
}

rule ContributingModels {
    from s : VPDL!Metamodel
    to t : VirtualLinks!ContributingModel (
         URI <- s.nsURI
       )
}

rule Filters {
    from s : VPDL!Attribute
    to
      f : VirtualLinks!Filter (
        name <- s.name,
        target <- c
      ),
      
      c : VirtualLinks!ConcreteElement (
          model <- s.refImmediateComposite().metamodel,
          path <- s.refImmediateComposite().class + '.' + s.name 
      )
}

rule Relations {
	from s : VPDL!Relation
	to
	  a : VirtualLinks!VirtualAssociation (
	      name <- s.name,
	      source <- source,
	      target <- target,
	      upperBound <- 1
	  ),
	  
	  source: VirtualLinks!ConcreteConcept (
	      model <- s.refImmediateComposite().metamodel,
          path <- s.refImmediateComposite().class
	  ),

	  target: VirtualLinks!ConcreteConcept (
	      model <- s.metamodelRight,
          path <- s.classRight
	  )
}
