grammar fr.inria.atlanmod.emfviews.vpdl.Vpdl with org.eclipse.xtext.common.Terminals
generate vpdl "http://www.inria.fr/atlanmod/emfviews/vpdl"

// Grammar for an SQL-like language to create model viewpoints
// to use with EMFViews

// TODO: allow uppercase keywords 

View: 'create' 'view' name=ID 'as'
  select=Select
  from=From
  where=Where?;
  
// TODO: allow wildcard '*'
Select: 'select' features+=SelectFeature (',' features+=SelectFeature)* ','?;

SelectFeature: metamodel=[Metamodel|ID] '.' class=ID '.' features+=Attribute
  | metamodel=[Metamodel|ID] '.' class=ID features+=Relation
  | metamodel=[Metamodel|ID] '.' class=ID '[' features+=Feature (',' features+=Feature)* ','? ']';

Feature: Attribute | Relation;

Attribute: name=ID;

Relation: 'join' metamodelRight=[Metamodel|ID] '.' classRight=ID 'as' name=ID;

From: 'from' metamodels+=Metamodel (','metamodels+=Metamodel)* ','?;

Metamodel: nsURI=STRING 'as' name=ID;



// TODO: allow aliases in WHERE, and arbitrary ECL expressions (or OCL?) 

Where: 'where' condition+=Condition+;

Condition:(value+=Comparison | value+=EclExpression) (('and' | 'or') 
  (value+=Comparison | value+=EclExpression))*;
  
EclExpression: value=STRING;

Comparison:
  left+=Left ('=' | '<=' | '>=' | '<' | '>' | '<>') right+=Right;
  
Left: metamodel+=[Metamodel|ID] '.' class+=ID '.' attributeWhereLeft=ID;
   
Right: (metamodel+=[Metamodel|ID] '.'class+=ID '.' attributeWhereRight=ID | value=STRING);
  
